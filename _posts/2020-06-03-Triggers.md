---

title:  "PL/SQL Triggers & ORACLE PACKAGE & Error Handling(Exception)"

layout: post

---

# DAY 35

## [오늘 할 내용]
1. Triggers
2. ORACLE PACKAGE
3. Error Handling(Exception)
4. 생성한 FUNCTION을 JDBC로 실행시키기

***
<https://docs.oracle.com/cd/E11882_01/appdev.112/e25519/triggers.htm#LNPLS99955>

### [9-1] Trigger Uses Conditional Predicates to Detect Triggering Statement

~~~ sql
CREATE OR REPLACE TRIGGER t
  BEFORE
    INSERT OR
    UPDATE OF SAL, DEPTNO OR
    DELETE
  ON RES_EMP
BEGIN
  CASE
    WHEN INSERTING THEN
      DBMS_OUTPUT.PUT_LINE('Inserting');
    WHEN UPDATING('SAL') THEN
      DBMS_OUTPUT.PUT_LINE('Updating salary');
    WHEN UPDATING('DEPTNO') THEN
      DBMS_OUTPUT.PUT_LINE('Updating department ID');
    WHEN DELETING THEN
      DBMS_OUTPUT.PUT_LINE('Deleting');
  END CASE;
END;
/
~~~

~~~ sql
INSERT INTO RES_EMP(EMPNO, ENAME) VALUES(1234, 'SSSS');
~~~
![image](https://user-images.githubusercontent.com/52989294/83583735-1b431700-a580-11ea-8a8d-f87e3df47937.png)

~~~ sql
UPDATE RES_EMP SET SAL = 1111 WHERE EMPNO = 7934;
~~~

![image](https://user-images.githubusercontent.com/52989294/83596122-cfec3100-a59e-11ea-8bae-c73cf7b11019.png)

~~~ sql
DELETE FROM RES_EMP WHERE EMPNO = 1234;
~~~

![image](https://user-images.githubusercontent.com/52989294/83596216-117cdc00-a59f-11ea-8328-99727478392b.png)

### [9-2] Trigger Logs Changes to EMP.SAL
~~~ sql
DROP TABLE Emp_log;

CREATE TABLE Emp_log (
  Emp_id     NUMBER,
  Log_date   DATE,
  New_salary NUMBER,
  Action     VARCHAR2(20));
~~~

~~~ sql
DROP TRIGGER log_salary_increase;

-- 트리거 생성하기
CREATE OR REPLACE TRIGGER log_salary_increase
  AFTER UPDATE OF SAL ON EMP
  FOR EACH ROW
BEGIN
  INSERT INTO Emp_log (Emp_id, Log_date, New_salary, Action)
  VALUES (:NEW.EMPNO, SYSDATE, :NEW.SAL, 'New Salary');
END;
/
~~~

~~~ sql
-- DEPTNO가 20인 사원들의 봉급 1111씩 추가
UPDATE EMP
SET SAL = SAL + 1111.0
WHERE DEPTNO = 20;
~~~

~~~ sql
-- 출력 확인
SELECT * FROM Emp_log;
~~~

![image](https://user-images.githubusercontent.com/52989294/83621120-3a19cb80-a5c9-11ea-81c0-489075363944.png)


### [9-3] Conditional Trigger Prints Salary Change Information
~~~ sql
DROP TRIGGER print_salary_changes;

CREATE OR REPLACE TRIGGER print_salary_changes
  BEFORE DELETE OR INSERT OR UPDATE ON EMP
  FOR EACH ROW
  WHEN (NEW.JOB <> 'PRESIDENT')  -- 직업이 PRESIDENT인 튜플 제외
DECLARE
  sal_diff  NUMBER;
BEGIN
  sal_diff  := :NEW.SAL  - :OLD.SAL;
  DBMS_OUTPUT.PUT(:NEW.ENAME || ': ');
  DBMS_OUTPUT.PUT('Old salary = ' || :OLD.SAL || ', ');
  DBMS_OUTPUT.PUT('New salary = ' || :NEW.SAL || ', ');
  DBMS_OUTPUT.PUT_LINE('Difference: ' || sal_diff);
END;
/
~~~

~~~ sql
-- 조회할 때는 PRESIDENT 정보가 출력됨
SELECT ENAME, DEPTNO, SAL, JOB
FROM EMP
WHERE DEPTNO IN (10, 20, 90)
ORDER BY DEPTNO, ENAME;
~~~

![image](https://user-images.githubusercontent.com/52989294/83621880-3cc8f080-a5ca-11ea-9b9d-d52c2eb19660.png)

~~~ sql
-- UPDATE를 하면 PRESIDENT 튜플을 제외한 내용만 출력
UPDATE EMP
SET SAL = SAL * 1.05
WHERE DEPTNO IN (10, 20, 90);
~~~

![image](https://user-images.githubusercontent.com/52989294/83622343-cbd60880-a5ca-11ea-9403-b816a1033e29.png)

### [9-5] Trigger with REFERENCING Clause
~~~ sql
-- 테이블 이름이 NEW일 때 키워드NEW 와 겹치기 때문에 별칭 사용
CREATE TABLE new (
  field1  NUMBER,
  field2  VARCHAR2(20)
);

CREATE OR REPLACE TRIGGER Print_salary_changes
BEFORE UPDATE ON new
REFERENCING new AS Newest -- 키워드 new를 Newest로 변경해서 사용하겠다.
FOR EACH ROW
BEGIN
  :Newest.Field2 := TO_CHAR (:newest.field1);
END;
/
~~~

***

<https://docs.oracle.com/cd/E11882_01/appdev.112/e25519/triggers.htm#LNPLS752>

### [9-6] Trigger References OBJECT_VALUE Pseudocolumn
~~~ sql
-- 자주 사용하는 컬럼들 선언(AS OBJECT)해서 나만의 TYPE 만들기
CREATE OR REPLACE TYPE t AS OBJECT (n NUMBER, m NUMBER)
/

CREATE TABLE tbl OF t
/

BEGIN
  FOR j IN 1..5 LOOP
    INSERT INTO tbl VALUES (t(j, 0));
  END LOOP;
END;
/

SELECT * FROM tbl ORDER BY n;
~~~

![image](https://user-images.githubusercontent.com/52989294/83596793-8dc3ef00-a5a0-11ea-94ff-d083625f7f95.png)

***

## ORACLE PACKAGE(패키지)
<https://docs.oracle.com/cd/E11882_01/appdev.112/e25519/packages.htm#LNPLS009>

- 연관성 높은 함수나 PROCEDURE를 하나의 그룹으로 묶어서 관리하고 사용하는 것
- PACKAGE 는 PACKAGE 선언부(Spec)와 PACKAGE 몸체부(Body)로 구성
	- PACKAGE 선언부 : 해당 패키지에 사용될 함수나 프로시저, 변수 등에 대한 정의를 선언
	- PACKAGE 몸체부 : 선언부에 있는 정의된 식별에 실제 CODE로 구현되는 부분
	- 패키지 삭제
		- 패키지 선언부와 몸체부 모두 삭제 : DROP PACKAGE PACKAGE_NAME;
		- 패키지 몸체부만 삭제 : DROP PACKAGE BODY PACKAGE_NAME;

Q1. 사원 테이블에서 총 급여 합계와 평균 급여를 구하는 MY_PACKAGE를 만들어 실행하자.
~~~ sql
-- 1. 선언
CREATE OR REPLACE PACKAGE MY_PACKAGE
IS
PROCEDURE MY_SUM;
PROCEDURE MY_AVG;
END MY_PACKAGE;
/

-- 2. BODY 부분
CREATE OR REPLACE PACKAGE BODY MY_PACKAGE
IS

PROCEDURE MY_SUM
IS
CURSOR E_SUM 
IS 
SELECT SUM(NVL(SAL,0))
FROM EMP;
TOTAL_SUM NUMBER;

BEGIN
  OPEN E_SUM;
   FETCH E_SUM INTO TOTAL_SUM;
   DBMS_OUTPUT.PUT_LINE('봉급 합계 : ' || TOTAL_SUM);
  CLOSE E_SUM;
END MY_SUM;  -- 합계 구하는 PROCEDURE 끝

PROCEDURE MY_AVG -- 평균 구하는 PROCEDURE 시작
IS
CURSOR E_AVG IS 
SELECT AVG(NVL(SAL,0)) 
FROM EMP;

TOTAL_AVG NUMBER;
BEGIN
  OPEN E_AVG;
   FETCH E_AVG INTO TOTAL_AVG;
   DBMS_OUTPUT.PUT_LINE('봉급 평균 : ' || TOTAL_AVG);
  CLOSE E_AVG;
END MY_AVG; -- 평균 구하는 PROCEDURE 끝
END MY_PACKAGE;
/
~~~

~~~ sql
EXEC MY_PACKAGE.MY_SUM;

EXEC MY_PACKAGE.MY_AVG;
~~~

![image](https://user-images.githubusercontent.com/52989294/83587531-4a5e8600-a58a-11ea-80cd-57a710099e5e.png)


### [10-2] Passing Associative Array to Standalone Subprogram
~~~ sql
CREATE OR REPLACE PACKAGE aa_pkg IS
  TYPE aa_type IS TABLE OF INTEGER INDEX BY VARCHAR2(15);
END;
/
~~~

~~~ sql
CREATE OR REPLACE PROCEDURE print_aa (
  aa aa_pkg.aa_type
) IS
  i  VARCHAR2(15);
BEGIN
  i := aa.FIRST;
 
  WHILE i IS NOT NULL LOOP
    DBMS_OUTPUT.PUT_LINE (aa(i) || '  ' || i);
    i := aa.NEXT(i);
  END LOOP;
END;
/
~~~

~~~ sql
DECLARE
  aa_var  aa_pkg.aa_type;
BEGIN
  aa_var('zero') := 0;
  aa_var('one') := 1;
  aa_var('two') := 2;
  print_aa(aa_var);
END;
/
~~~

![image](https://user-images.githubusercontent.com/52989294/83622944-a4cc0680-a5cb-11ea-8008-097bdea7a204.png)

***

<https://docs.oracle.com/cd/E11882_01/appdev.112/e25519/packages.htm#LNPLS00905>

### [10-3] Matching Package Specification and Body
~~~ sql
DROP PACKAGE EMP_BONUS;

CREATE PACKAGE emp_bonus AS
  PROCEDURE calc_bonus (date_hired EMP.HIREDATE%TYPE);
END emp_bonus;
/
~~~

~~~ sql
CREATE OR REPLACE PACKAGE BODY emp_bonus AS
  PROCEDURE calc_bonus
    (date_hired EMP.HIREDATE%TYPE) IS
  BEGIN
    DBMS_OUTPUT.PUT_LINE
      ('Employees hired on ' || date_hired || ' get bonus.');
  END;
END emp_bonus;
/
~~~

## Error Handling(Exception)

<https://docs.oracle.com/cd/E11882_01/appdev.112/e25519/errors.htm#LNPLS00703>

### [11-6] Anonymous Block Handles ZERO_DIVIDE
~~~ sql
DECLARE
  stock_price   NUMBER := 9.73;
  net_earnings  NUMBER := 0;
  pe_ratio      NUMBER;
BEGIN
  pe_ratio := stock_price / net_earnings;  -- raises ZERO_DIVIDE exception
  DBMS_OUTPUT.PUT_LINE('Price/earnings ratio = ' || pe_ratio);
END;
/
~~~
### 에러 발생

![image](https://user-images.githubusercontent.com/52989294/83589236-32890100-a58e-11ea-9f0a-21fcec52b382.png)

~~~ sql
-- EXCEPTION 처리하기
DECLARE
  stock_price   NUMBER := 9.73;
  net_earnings  NUMBER := 0;
  pe_ratio      NUMBER;
BEGIN
  pe_ratio := stock_price / net_earnings;  -- raises ZERO_DIVIDE exception
  DBMS_OUTPUT.PUT_LINE('Price/earnings ratio = ' || pe_ratio);
EXCEPTION
  WHEN ZERO_DIVIDE THEN
    DBMS_OUTPUT.PUT_LINE('Company had zero earnings.');
    pe_ratio := NULL;
END;
/
~~~

***

### Raising Exceptions Explicitly
- 객체가 소멸될 때 자동으로 호출되는 단어

<https://docs.oracle.com/cd/E11882_01/appdev.112/e25519/errors.htm#LNPLS99869>

### [11-9] Declaring, Raising, and Handling User-Defined Exception
~~~ sql
CREATE PROCEDURE account_status (
  due_date DATE,
  today    DATE
) AUTHID DEFINER
IS
  past_due  EXCEPTION;  -- declare exception
BEGIN
  IF due_date < today THEN
    RAISE past_due;  -- explicitly raise exception
  END IF;
EXCEPTION
  WHEN past_due THEN  -- handle exception
    DBMS_OUTPUT.PUT_LINE ('Account past due.');
END;
/
 
BEGIN
  account_status ('1-07-10', '9-07-10');
END;
/
~~~

### [11-11] Reraising Exception
~~~ sql
DECLARE
  salary_too_high   EXCEPTION;
  current_salary    NUMBER := 20000;
  max_salary        NUMBER := 10000;
  erroneous_salary  NUMBER;
BEGIN

  BEGIN
    IF current_salary > max_salary THEN
      RAISE salary_too_high;   -- raise exception
    END IF;
  EXCEPTION
    WHEN salary_too_high THEN  -- start handling exception
      erroneous_salary := current_salary;
      DBMS_OUTPUT.PUT_LINE('Salary ' || erroneous_salary ||' is out of range.');
      DBMS_OUTPUT.PUT_LINE ('Maximum salary is ' || max_salary || '.');
      RAISE;  -- reraise current exception (exception name is optional)
  END;

EXCEPTION
  WHEN salary_too_high THEN    -- finish handling exception
    current_salary := max_salary;

    DBMS_OUTPUT.PUT_LINE (
      'Revising salary from ' || erroneous_salary ||
      ' to ' || current_salary || '.'
    );
END;
/
~~~

![image](https://user-images.githubusercontent.com/52989294/83590185-21d98a80-a590-11ea-8882-f2889c33959a.png)

- 처음 초록 박스에서 EXCEPTION 발생 시 다음 초록 박스로 이동 
- 이동해서 EXCEPTION 발생 시 두번째 빨간 박스로 이동해서 처리

***

## SQL 구문을 이용해서 프로시져를 만들자.
(big5 계정에서 진행)

### Q1.String myemp_all = "SELECT * FROM My_Emp";

~~~ sql
CREATE OR REPLACE PROCEDURE MYEMP_ALL(MYRES OUT SYS_REFCURSOR)
IS
BEGIN
  OPEN MYRES FOR SELECT * FROM My_Emp;
END;
/

-- 실행시켜보자.
VAR RES REFCURSOR;
EXEC MYEMP_ALL(:RES)

PRINT RES
~~~

### Q2.String myemp_vo = "SELECT * FROM MY_EMP WHERE ENAME = ?";

~~~ sql
CREATE OR REPLACE PROCEDURE myemp_vo(MYNAME IN MY_EMP.ENAME%TYPE, MYRES OUT SYS_REFCURSOR)
IS
BEGIN
OPEN MYRES FOR
SELECT * FROM MY_EMP WHERE ENAME = MYNAME;
END;
/

-- 실행시켜보자.
VAR RES REFCURSOR;
EXEC myemp_vo('SMITH', :RES)

PRINT RES;
~~~

### Q3. String myemp_vo2 = "SELECT * FROM MY_EMP WHERE ENAME = ? AND EMPNO = ?";

~~~ sql
CREATE OR REPLACE PROCEDURE myemp_vo2(MYNAME IN MY_EMP.ENAME%TYPE, MYEMPNO IN MY_EMP.EMPNO%TYPE, MYRES OUT SYS_REFCURSOR)
IS
BEGIN
  OPEN MYRES FOR 
  SELECT * FROM MY_EMP WHERE ENAME = MYNAME AND EMPNO = MYEMPNO;
END;
/

-- 실행시켜보자.
VAR RES REFCURSOR;
EXEC myemp_vo2('KING', 7839, :RES)

PRINT RES;
~~~

### Q4. String myemp_insert = "INSERT INTO MY_EMP VALUES(?,?,?)";

~~~ sql
CREATE OR REPLACE PROCEDURE myemp_insert(MYEMPNO IN MY_EMP.EMPNO%TYPE,MYNAME IN MY_EMP.ENAME%TYPE, MYDEPTNO IN MY_EMP.DEPTNO%TYPE)
IS
BEGIN
  INSERT INTO MY_EMP VALUES(MYEMPNO,MYNAME,MYDEPTNO);
END;
/

-- 실행시켜보자.
EXEC myemp_insert(4444,'ADSP',11)

SELECT * FROM MY_EMP;
~~~

### Q5.String myemp_delete = "DELETE FROM MY_EMP WHERE EMPNO = ?";

~~~ sql
CREATE OR REPLACE PROCEDURE myemp_delete(MYEMPNO IN MY_EMP.EMPNO%TYPE)
IS
BEGIN
  DELETE FROM MY_EMP WHERE EMPNO = MYEMPNO;
END;
/

-- 실행시켜보자.
EXEC myemp_delete(4444)

SELECT * FROM MY_EMP;
~~~

### Q6. String myemp_update = "UPDATE MY_EMP SET ENAME = ?, DEPTNO = ? WHERE EMPNO = ?";

~~~ sql
CREATE OR REPLACE PROCEDURE myemp_update(MYENAME IN MY_EMP.ENAME%TYPE,
MYDEPTNO IN MY_EMP.DEPTNO%TYPE, MYEMPNO IN MY_EMP.EMPNO%TYPE)
IS
BEGIN
  UPDATE MY_EMP SET ENAME = MYENAME, DEPTNO = MYDEPTNO WHERE EMPNO = MYEMPNO;
END;
/

-- 실행시켜보자.
EXEC myemp_update('JJJ', 10,222);

SELECT * FROM MY_EMP;
~~~

***

<https://docs.oracle.com/cd/E11882_01/java.112/e16548/overvw.htm#JJDBC28037>

![image](https://user-images.githubusercontent.com/52989294/83599267-b0f19d00-a5a6-11ea-9158-06214fe6ba0f.png)

<https://docs.oracle.com/cd/E17781_01/appdev.112/e18805/addfunc.htm#TDPJD206>

MyEmpDAO 나머지도 수정할 것
(중요)

***

## BIG6에 FUNCTION을 생성하고 JAVA로 호출해보자
<https://docs.oracle.com/cd/E17781_01/appdev.112/e18805/addfunc.htm#TDPJD211>

~~~ sql
SELECT OBJECT_NAME FROM USER_PROCEDURES;  -- 프로시져 확인

-- FUNCTION 생성
CREATE OR REPLACE FUNCTION FOO (VAL1 CHAR)
RETURN CHAR AS
BEGIN
RETURN VAL1 || ' SUFFIX';
END;
/

SELECT * FROM USER_SOURCE WHERE NAME = 'FOO'; -- 소스 확인
~~~
![image](https://user-images.githubusercontent.com/52989294/83616754-2cf9de00-a5c3-11ea-8fe7-ca8e702d26af.png)

### Eclipse 실행

~~~ java
package com.test;

import java.sql.CallableStatement;
import java.sql.Connection;
import java.sql.SQLException;
import java.sql.Types;

import oracle.jdbc.pool.OracleDataSource;

public class MTest {
	public static void main(String[] args) throws SQLException {
		OracleDataSource ods = new OracleDataSource();
		ods.setURL("jdbc:oracle:thin:@localhost:1521:XE");
		ods.setUser("big6");
		ods.setPassword("admin1234");
		Connection conn = ods.getConnection();

		System.out.println(conn.isClosed()); // 중간에 닫혔나 확인

		CallableStatement cs =
				conn.prepareCall("begin ? := foo(?); end;");
				// 또는 conn.prepareCall("{call ? := foo(?)}");
                
		cs.registerOutParameter(1, Types.CHAR);
		cs.setString(2, "aa");
		cs.executeUpdate();
		String result = cs.getString(1);

		System.out.println(result);

		conn.close();
	}
}

// 출력결과 false 
//		  aa SUFFIX
~~~

***


## BIG5에 FUNCTION을 생성하고 JAVA로 호출해보자

### 1. JDBC GET CONNECTION

~~~ java
package common;

import java.sql.Connection;
import java.sql.DriverManager;
import java.sql.ResultSet;
import java.sql.SQLException;
import java.sql.Statement;

////// Connection & Close & Commit & rollback  4개만 user가 제어할 수 있게 만들어둠
public class JDBCTemplate {
	// 연결한 상태로 리턴해주겠다.
	public static Connection getConnection() {
		try {
			Class.forName("oracle.jdbc.OracleDriver");
		} catch (ClassNotFoundException e) {
			e.printStackTrace();
		}

		String url = "jdbc:oracle:thin:@localhost:1521:XE";
		String id = "big5";
		String pwd = "admin1234";
		Connection con = null;

		try {
			con = DriverManager.getConnection(url, id, pwd);
			con.setAutoCommit(false);
		} catch (SQLException e) {
			e.printStackTrace();
		}
		return con;
	}

	// 연결 객체를 받아서(연결한 상태를) close()하겠다.
	public static void Close(Connection con) {
		try {
			if (!con.isClosed() && con != null) {
				con.close();
			}
		} catch (SQLException e) {
			e.printStackTrace();
		}
	}

	public static void Close(Statement stmt) {
		try {
			if (stmt != null) {
				stmt.close();
			}
		} catch (SQLException e) {
			e.printStackTrace();
		}
	}

	public static void Close(ResultSet rs) {
		try {
			if (rs != null) {
				rs.close();
			}
		} catch (SQLException e) {
			e.printStackTrace();
		}
	}

	public static void Commit(Connection con) {
		try {
			if (con != null && !con.isClosed()) {
				con.commit();
			}
		} catch (SQLException e) {
			e.printStackTrace();
		}
	}

	public static void Rollback(Connection con) {
		try {
			if (con != null && !con.isClosed()) {
				con.rollback();
			}
		} catch (SQLException e) {
			e.printStackTrace();
		}
	}

}
~~~

### 2.사번을 입력 받아서 부서명을 리턴하는 함수를 작성

~~~ sql
CREATE FUNCTION getDept(P_EMPNO IN EMP.EMPNO%TYPE) RETURN VARCHAR2 
--문자열로 리턴하겠다.
IS
V_DEPTNO EMP.DEPTNO%TYPE;  --SELECT 문에서 리턴을 하는 부서번호 대입
V_RES VARCHAR2(60); -- 출력구문, 부서명을 저장하는 변수

BEGIN
SELECT DEPTNO INTO V_DEPTNO FROM EMP
WHERE EMPNO = P_EMPNO;  -- 사원번호에 해당하는 부서번호 출력

IF V_DEPTNO = 10 THEN
V_RES := 'ACCOUNT 부서 사원입니다.';
ELSIF V_DEPTNO = 20 THEN
V_RES := 'RESEARCH 부서 사원입니다.';
ELSIF V_DEPTNO = 30 THEN
V_RES := 'SALES 부서 사원입니다.';
ELSIF V_DEPTNO = 40 THEN
V_RES := 'OPERATIONS 부서 사원입니다.';
END IF;

RETURN V_RES;
END;
/
~~~

### 3. JAVA 코드 작성
~~~ java
package com.test;

import java.sql.*;
import static common.JDBCTemplate.*;

public class MTest02 {
	public static void main(String[] args) throws SQLException {
		Connection con = getConnection();

		CallableStatement cs = con.prepareCall("begin ? := getDept(?); end;");
		// 또는 con.prepareCall("{call ? := getDept(?)}");
		cs.registerOutParameter(1, Types.VARCHAR);
		cs.setInt(2, 7900);
		cs.executeUpdate();

		String result = cs.getString(1);

		System.out.println(result);

		Close(con);
	}
}

// 출력값 : SALES 부서 사원입니다.
~~~

















































